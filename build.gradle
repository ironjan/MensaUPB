def versionMajor = 0 // incompatible API changes
def versionMinor = 1 // added functionality in a backwards-compatible manner
def versionPatch = 0 // backwards-compatible bug fixes
def versionBuild = 0 // bump for dogfood builds, public betas, etc.

buildscript {
    repositories {
        mavenCentral()
        maven { url 'http://repo1.maven.org/maven2' }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:0.9.0'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.1'
    }
}

repositories {
    mavenCentral()
}

apply plugin: 'android'
apply plugin: 'android-apt'

configurations {
    apt
    unitTestCompile.extendsFrom runtime
    unitTestRuntime.extendsFrom unitTestCompile
}


def getSourceSetName(variant) {
    return new File(variant.dirName).getName();
}


android {
    compileSdkVersion 19
    buildToolsVersion "19"

    packagingOptions {
        // prevents Error: duplicate files during packaging of APK
        // no wildcard support yet
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
    }

    // This is important, it will run lint checks but won't abort build
    lintOptions {
        abortOnError false
    }


    defaultConfig {
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        minSdkVersion 8
        targetSdkVersion 19

        packageName "de.najidev.mensaupb"
    }


    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
        }

    }

    buildTypes {
        debug {
            buildConfigField "int", "SYNC_INTERVAL", "60" // 1 minute
        }

        release {
            buildConfigField "int", "SYNC_INTERVAL", "60 * 60 * 12" // 12 hours
        }
    }

    if (project.hasProperty("mensaupb.signing")
            && new File(project.property("mensaupb.signing") + ".gradle").exists()) {
        apply from: project.property("mensaupb.signing") + ".gradle";
    }

}

dependencies {
    compile 'com.android.support:support-v4:19.0.0'
    compile 'com.android.support:appcompat-v7:19.0.0'

    compile 'com.j256.ormlite:ormlite-android:4.47'

    apt "org.androidannotations:androidannotations:3.0"
    compile "org.androidannotations:androidannotations-api:3.0"

    compile fileTree(dir: 'libs', include: '*.jar')

//     tests
    unitTestCompile files("$project.buildDir/classes/release")
    unitTestCompile 'junit:junit:4.11'

//    tests support in AS
    androidTestCompile 'junit:junit:4.11'
}

sourceSets {
        unitTest {
            java.srcDir file('src/test/java')
            resources.srcDir file('src/test/resources')
        }
}

task unitTest(type:Test, dependsOn: assemble){
    description = 'run unit tests'
    testClassesDir = project.sourceSets.unitTest.output.classesDir
    classpath = project.sourceSets.unitTest.runtimeClasspath
}

check.dependsOn unitTest

task wrapper(type: Wrapper) {
    gradleVersion = '1.9'
}
