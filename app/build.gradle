def versionMajor = 2 // incompatible API changes
def versionMinor = 1 // added functionality in a backwards-compatible manner
def versionPatch = 7 // backwards-compatible bug fixes
def versionBuild = 0 // bump for dogfood builds, public betas, etc.


repositories {
    mavenCentral()
}

apply plugin: 'com.android.application'
apply plugin: 'android-apt'
//apply plugin: 'android-test'
apply plugin: 'idea'

configurations {
    apt
    unitTestCompile.extendsFrom runtime
    unitTestRuntime.extendsFrom unitTestCompile
}


def getSourceSetName(variant) {
    return new File(variant.dirName).getName();
}


android {
    compileSdkVersion 20
    buildToolsVersion '20.0.0'

    packagingOptions {
        // prevents Error: duplicate files during packaging of APK
        // no wildcard support yet
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'LICENSE.txt'
    }

    // This is important, it will run lint checks but won't abort build
    lintOptions {
        abortOnError false
    }


    defaultConfig {
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        minSdkVersion 8
        targetSdkVersion 20

        applicationId "de.ironjan.mensaupb"
    }


    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
        }
    }

    buildTypes {
        debug {
            buildConfigField "int", "SYNC_INTERVAL", "60" // 1 minute
            buildConfigField "String", "STW_URL", "\"\""
        }

        release {
            buildConfigField "int", "SYNC_INTERVAL", "60 * 60 * 24 * 2" // 2 days
            buildConfigField "String", "STW_URL", "\"\""
        }
    }

    if (project.hasProperty("mensaupb.signing")) {
        println "project has property mensaupb.signing:"
        println project.property("mensaupb.signing")
        if (new File(project.property("mensaupb.signing")).exists()) {
            apply from: project.property("mensaupb.signing");
            println "Applied properties"

        }
    }
}

dependencies {
    compile 'com.android.support:support-v4:20.0.0'
    compile 'com.android.support:appcompat-v7:20.0.0'
    compile 'com.j256.ormlite:ormlite-android:4.48'
    apt "org.androidannotations:androidannotations:3.1"
    compile 'org.androidannotations:androidannotations-api:3.1'
    compile 'com.noveogroup.android:android-logger:1.3.4'
    compile fileTree(dir: 'libs', include: '*.jar')
    //     tests
    //    androidTestCompile 'junit:junit:4.11'
    //    androidTestCompile 'org.robolectric:robolectric:2.3'
    //    androidTestCompile 'com.squareup:fest-android:1.0.0'
    compile project(':library')
    compile 'com.koushikdutta.ion:ion:2.+'
}


task wrapper(type: Wrapper) {
    gradleVersion = '1.9'
}

