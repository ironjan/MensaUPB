// incompatible API changes
def versionMajor = 2
// added functionality in a backwards-compatible manner, must be <=99
def versionMinor = 21
// backwards-compatible bug fixes, must be <=99
def versionPatch = 4
// bump for dogfood builds, public betas, etc., must be <=99
def versionBuild = 0


apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'idea'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.getkeepsafe.dexcount'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    packagingOptions {
        // prevents Error: duplicate files during packaging of APK
        // no wildcard support yet
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'LICENSE.txt'
    }
    defaultConfig {
        versionCode versionMajor * 1000000 + versionMinor * 10000 + versionPatch * 100 + versionBuild

        if (versionBuild != 0) {
            versionName "${versionMajor}.${versionMinor}.${versionPatch}-${versionBuild}"
        } else {
            versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        }

        minSdkVersion 14
        targetSdkVersion 28

        applicationId 'de.ironjan.mensaupb'

        multiDexEnabled true


        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ['resourcePackageName': 'de.ironjan.mensaupb']
            }
        }

        resValue "string", "authority", applicationId + '.provider'
        resValue "string", "account_type", applicationId + ".account"
        buildConfigField "String", "ACCOUNT_TYPE", '"' + applicationId + '.account' + '"'
    }
    sourceSets {
        androidTest.setRoot('src/test')
        main.java.srcDirs += 'src/main/kotlin'
    }
    buildTypes {
        debug {
            buildConfigField 'int', 'SYNC_INTERVAL', '60' // 1 minute
            buildConfigField 'String', 'STW_URL', '\"\"'
            testCoverageEnabled true
            applicationIdSuffix ".debug"
        }
        release {
            debuggable false
            jniDebuggable false
        }
        println 'Applied basic configurations.'
    }
    lintOptions {
        disable "InvalidPackage"
    }
    // must be set after basic configurations
    if (project.hasProperty('mensaupb.signing')) {
        println 'project has property mensaupb.signing:'
        println project.property('mensaupb.signing')
        if (new File(project.property('mensaupb.signing')).exists()) {
            apply from: project.property('mensaupb.signing')
            println 'Applied properties'
        } else {
            println 'Could not find signing config'
        }
    } else {
        println 'Could not find signing property.'
    }
    productFlavors {
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
}


dependencies {

    implementation 'com.android.support:multidex:1.0.3'

    implementation 'com.android.support:support-v4:28.0.0'
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.j256.ormlite:ormlite-android:5.0'
    implementation 'com.noveogroup.android:android-logger:1.3.6'
    implementation 'com.koushikdutta.ion:ion:2.2.1'
    implementation 'se.emilsjolander:stickylistheaders:2.7.0'


    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.4.0'

    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.14'

    // Android annotations and dependendencies
    annotationProcessor "org.androidannotations:androidannotations:$androidannotations_version"
    implementation "org.androidannotations:androidannotations-api:$androidannotations_version"
    annotationProcessor "org.androidannotations:rest-spring:$androidannotations_version"
    implementation "org.androidannotations:rest-spring-api:$androidannotations_version"
    kapt "org.androidannotations:androidannotations:$androidannotations_version"

    // current jackson version is 2.7.x but this version introduced a call to java.nio.file
    // which is not supported on android
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.9.1'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.9.1'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.9.1'
    implementation 'org.springframework.android:spring-android-rest-template:2.0.0.M3'

    implementation fileTree(include: '*.jar', dir: 'libs')

    implementation "com.github.kittinunf.fuel:fuel:$fuel_version" //for JVM
    implementation "com.github.kittinunf.fuel:fuel-android:$fuel_version" //for Android
    implementation "com.github.kittinunf.fuel:fuel-rxjava:$fuel_version" //for RxJava support
    implementation "com.github.kittinunf.fuel:fuel-gson:$fuel_version" //for Gson support
    implementation "io.arrow-kt:arrow-core:$arrow_version"


    // Test dependencies
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.robolectric:robolectric:3.4.2'
    testImplementation 'org.robolectric:shadows-support-v4:3.3.2'
    // Repetition for autocompletion in android studio
    androidTestImplementation 'junit:junit:4.12'
    androidTestImplementation 'org.robolectric:robolectric:3.4.2'
    androidTestImplementation 'org.robolectric:shadows-support-v4:3.3.2'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}
repositories {
    mavenCentral()
}

