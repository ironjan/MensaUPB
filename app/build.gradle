// incompatible API changes
def versionMajor = 2
// added functionality in a backwards-compatible manner, must be <=99
def versionMinor = 18
// backwards-compatible bug fixes, must be <=99
def versionPatch = 8
// bump for dogfood builds, public betas, etc., must be <=99
def versionBuild = 0


apply plugin: 'com.android.application'
apply plugin: 'android-apt'
apply plugin: 'idea'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.getkeepsafe.dexcount'

configurations {
    apt
}

android {
    signingConfigs {
    }
    compileSdkVersion 25
    buildToolsVersion '25.0.2'
    packagingOptions {
        // prevents Error: duplicate files during packaging of APK
        // no wildcard support yet
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'LICENSE.txt'
    }
    defaultConfig {
        versionCode versionMajor * 1000000 + versionMinor * 10000 + versionPatch * 100 + versionBuild

        if (versionBuild != 0) {
            versionName "${versionMajor}.${versionMinor}.${versionPatch}-${versionBuild}"
        } else {
            versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        }

        minSdkVersion 14
        targetSdkVersion 23

        applicationId 'de.ironjan.mensaupb'
    }
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
        }

        androidTest.setRoot('src/test')
    }
    buildTypes {
        debug {
            buildConfigField 'int', 'SYNC_INTERVAL', '60' // 1 minute
            buildConfigField 'String', 'STW_URL', '\"\"'
            testCoverageEnabled true
        }
        release {
            buildConfigField 'int', 'SYNC_INTERVAL', '60 * 60 * 24 ' // 1 day
            buildConfigField 'String', 'STW_URL', '\"\"'
            debuggable false
            jniDebuggable false
        }
        println 'Applied basic configurations.'
    }
    lintOptions {
        disable "InvalidPackage"
    }
    // must be set after basic configurations
    if (project.hasProperty('mensaupb.signing')) {
        println 'project has property mensaupb.signing:'
        println project.property('mensaupb.signing')
        if (new File(project.property('mensaupb.signing')).exists()) {
            apply from: project.property('mensaupb.signing');
            println 'Applied properties'
        } else {
            println 'Could not find signing config';
        }
    } else {
        println 'Could not find signing property.';
    }
    productFlavors {
    }
}

dependencies {
    compile 'com.android.support:support-v4:25.3.0'
    compile 'com.android.support:appcompat-v7:25.3.0'
    compile 'com.j256.ormlite:ormlite-android:5.0'
    apt 'org.androidannotations:androidannotations:4.3.1'
    compile 'org.androidannotations:androidannotations-api:4.3.1'
    apt "org.androidannotations:rest-spring:4.3.1"
    compile "org.androidannotations:rest-spring:4.3.1"
    compile 'com.noveogroup.android:android-logger:1.3.6'
    compile 'se.emilsjolander:stickylistheaders:2.7.0'
    compile 'com.koushikdutta.ion:ion:2.2.1'

    // current jackson version is 2.7.x but this version introduced a call to java.nio.file
    // which is not supported on android
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.9.1'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.9.1'
    compile 'com.fasterxml.jackson.core:jackson-core:2.9.1'

    compile 'org.springframework.android:spring-android-rest-template:1.0.1.RELEASE'

    compile fileTree(include: '*.jar', dir: 'libs')
    testCompile 'junit:junit:4.12'
    testCompile 'org.robolectric:robolectric:3.0'
    testCompile 'org.robolectric:shadows-support-v4:3.0'
    // Repetition for autocompletion in android studio
    androidTestCompile 'junit:junit:4.12'
    androidTestCompile 'org.robolectric:robolectric:3.0'
    testCompile 'org.robolectric:shadows-support-v4:3.0'
}

